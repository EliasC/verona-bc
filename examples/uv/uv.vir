lib
  @printval = "printval"(dyn): none
  @puts = "puts"(ptr): i32
  @uv_os_homedir = "uv_os_homedir"(ptr, ptr): i32
  @uv_default_loop = "uv_default_loop"(): ptr
  @uv_handle_create = "uv_handle_create"(i32): ptr
  @uv_handle_closure = "uv_handle_closure"(ptr, dyn): none
  @uv_callback_closure = "uv_callback_closure"(ptr): none
  @uv_close_closure = "uv_close_closure"(ptr): none
  @uv_timer_init = "uv_timer_init"(ptr, ptr): i32
  @uv_timer_start = "uv_timer_start"(ptr, ptr, u64, u64): i32
  @uv_timer_stop = "uv_timer_stop"(ptr): i32

class @timer
  @handle: ptr
  @counter: usize
  @apply @timer_apply

func @timer_apply($self: @timer): none
  $one = const usize 1
  $done = const usize 3
  $counter_ref = ref copy $self @counter
  $counter = load $counter_ref
  $cond = ge $counter $done
  cond $cond ^done ^not_done
^done
  $handle_ref = ref copy $self @handle
  $handle = load $handle_ref
  $_ = ffi @uv_timer_stop(copy $handle)
  $_ = ffi @uv_close_closure(move $handle)
  jump ^return
^not_done
  $counter = add $counter $one
  $_ = ffi @printval(copy $counter)
  $_ = store $counter_ref copy $counter
  jump ^return
^return
  $none = const none
  ret $none

func @print_home(): none
  $home = stack u8[256]
  $home_ptr = makeptr $home
  $home_len = len $home
  $home_len_ptr = makeptr $home_len
  $_ = ffi @uv_os_homedir(copy $home_ptr, move $home_len_ptr)
  $_ = ffi @puts(copy $home_ptr)
  $ret = const none
  ret $ret

func @main(): i32
  $_ = call @print_home()
  $timer_type = const i32 13
  $timer_ptr = ffi @uv_handle_create(move $timer_type)
  $loop = ffi @uv_default_loop()
  $_ = ffi @uv_timer_init(copy $loop, copy $timer_ptr)
  $counter = const usize 0
  $timer = region rc @timer(copy $timer_ptr, move $counter)
  $_ = ffi @uv_handle_closure(copy $timer_ptr, move $timer)
  $timeout = const u64 1000
  $callback = lookup @uv_callback_closure
  $_ = ffi @uv_timer_start(
    copy $timer_ptr, move $callback, copy $timeout, copy $timeout)
  $ret = const i32 0
  ret $ret
